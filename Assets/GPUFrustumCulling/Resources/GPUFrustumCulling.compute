#pragma kernel CSMain
#define FRUSTUM_PLANE_COUNT 6
#define THREAD_COUNT 64

struct BufferData
{
    float3 center;
    float3 extents;
};

StructuredBuffer<BufferData> boundsBuffer;
RWStructuredBuffer<uint> resultBuffer;
float4 frustumPlanes[FRUSTUM_PLANE_COUNT];

void TestPlanesAABBInternalFast(uint id, BufferData bufferData)
{
    float3 center = bufferData.center;
    float3 extents = bufferData.extents;
    float3 boundsMin = center - extents;
    float3 boundsMax = center + extents;
    
    for(int i = 0; i < FRUSTUM_PLANE_COUNT; i++)
    {
        float3 normal = frustumPlanes[i].xyz;
        float planeDistance = frustumPlanes[i].w;
        float3 bounds = lerp(boundsMin, boundsMax, step(0, normal));
        
        if(dot(normal, bounds) + planeDistance < 0)
        {
            resultBuffer[id] = 0;
            return;
        }
    }
    
    resultBuffer[id] = 1;
}

[numthreads(THREAD_COUNT,1,1)]
void CSMain (uint id : SV_DispatchThreadID)
{
    TestPlanesAABBInternalFast(id, boundsBuffer[id]);
}
